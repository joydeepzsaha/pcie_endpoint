//! @title pcie_ltssm_downstream
//! @author Idris Somoye
//! Module implements the pcie physical layer link training state machine.
//! master axis bus.
//!
//! Module does not support upconfig!
//!
//! Module does not support crosslink!
//!
//! Module does not support autonomous lane-width reconfiguration: a link
//! width change requires a full retrain from Detect (there is no live-link
//! path that renegotiates width without first dropping back through
//! Detect/Configuration).
//!
//! Module does not support lane reversal.
module pcie_ltssm_downstream
  import pcie_phy_pkg::*;
#(
    parameter int CLK_RATE      = 100,                //!Clock speed in MHz, Defualt is 100
    parameter int MAX_NUM_LANES = 4,                  //! Maximum number of lanes module can support
    // TLP data width
    parameter int DATA_WIDTH    = 32,                 //! AXIS data width
    // TLP keep width
    parameter int KEEP_WIDTH    = DATA_WIDTH / 8,
    parameter int USER_WIDTH    = $bits(phy_user_t),
    parameter int SIM_FAST_LINK = 0,

    parameter int          IS_ROOT_PORT = 0,
    parameter int          LINK_NUM           = 0,
    parameter int          IS_UPSTREAM        = 0,    //downstream by default
    parameter int          CROSSLINK_EN       = 0,    //crosslink not supported
    parameter int          UPCONFIG_EN        = 0,    //upconfig not supported
    parameter rate_speed_e MAX_SUPPORTED_RATE = gen1
) (
    input  logic                         clk_i,                //! 100MHz clock signal
    input  logic                         rst_i,                //! Reset signal
    // !Control
    input  logic                         en_i,
    output logic                         link_up_o,
    input  logic                         is_timeout_i,
    input  logic                         recovery_i,
    output logic                         error_o,
    output logic                         success_o,
    output logic                         error_loopback_o,
    output logic                         error_disable_o,
    input  logic [    MAX_NUM_LANES-1:0] ts1_valid_i,
    input  logic [    MAX_NUM_LANES-1:0] ts2_valid_i,
    input  logic [    MAX_NUM_LANES-1:0] idle_valid_i,
    input  logic [    MAX_NUM_LANES-1:0] polarity_inverted_i,
    input  logic [(MAX_NUM_LANES*3)-1:0] phy_rxstatus_i,
    input  logic [    MAX_NUM_LANES-1:0] phy_phystatus_i,
    input  logic                         phy_phystatus_rst_i,
    output logic                         phy_txdetectrx_o,

    output logic [MAX_NUM_LANES-1:0] phy_txelecidle_o,
    output logic                     phy_txdeemph_o,
    output logic [              1:0] phy_powerdown_o,
    output logic                     phy_txcompliance_o,
    output logic [MAX_NUM_LANES-1:0] phy_rxpolarity_o,
    output logic [              2:0] phy_txmargin_o,
    // input  logic [      MAX_NUM_LANES-1:0] lane_active_i,
    input  logic [MAX_NUM_LANES-1:0] lanes_ts2_satisfied_i,
    input  logic [MAX_NUM_LANES-1:0] config_copmlete_ts2_i,
    input  logic                     from_l0_i,

    // Holds all lanes where a Receiver has been detected
    input  logic [MAX_NUM_LANES-1:0] receiver_detected_i,

    // Holds all lanes where Receiver is in EI
    // These should all act together for use???
    input  logic [MAX_NUM_LANES-1:0] phy_rxelecidle_i,

    output logic [MAX_NUM_LANES-1:0] tx_enter_elec_idle_o,
    output logic [              19:0] ltssm_state_o,
    output logic                     goto_cfg_o,
    output logic                     goto_detect_o,
    input  logic                     ordered_set_tranmitted_i,
    output logic                     send_ordered_set_o,
    output logic [MAX_NUM_LANES-1:0] active_lanes_o,

    output gen_os_struct_t                        gen_os_ctrl_o,
    //training set configuration signals
    input  pcie_tsos_t        [MAX_NUM_LANES-1:0] ordered_set_i,
    output presets_coeff_t    [MAX_NUM_LANES-1:0] preset_coeff_o,
    output pcie_ordered_set_t                     ordered_set_o,
    // input  ts_symbol6_union_t [MAX_NUM_LANES-1:0] symbol6_i,
    // input  training_ctrl_t    [MAX_NUM_LANES-1:0] training_ctrl_i,
    // input  rate_id_t          [MAX_NUM_LANES-1:0] rate_id_i,
    input  logic                                  extended_synch_i,
    // output logic                                  gen_os_o,
    //TODO: this needs to be computed from ts1's/ ts2's with
    //speed change bit or sw active
    input  logic                                  directed_speed_change_i,
    input  logic              [MAX_NUM_LANES-1:0] lane_status_i,
    output rate_speed_e                           curr_data_rate_o,
    output rate_id_t                              data_rate_o,
    output logic                                  changed_speed_recovery_o
    // //! @virtualbus master_axis_bus @dir out
    // output logic              [   DATA_WIDTH-1:0] m_axis_tdata,
    // output logic              [   KEEP_WIDTH-1:0] m_axis_tkeep,
    // output logic                                  m_axis_tvalid,
    // output logic                                  m_axis_tlast,
    // output logic              [   USER_WIDTH-1:0] m_axis_tuser,
    // input  logic                                  m_axis_tready
    //! @end
);

  localparam int ClockPeriodNs = ((10 ** 3) / CLK_RATE);
  localparam longint TwentyFourMsTimeOut = (24 * (10 ** 6)) / ClockPeriodNs;
  localparam longint FourtyEightMsTimeOut = (48 * (10 ** 6)) / ClockPeriodNs;
  localparam longint TwelveMsTimeOut = SIM_FAST_LINK ? (12 * (10 ** 4)) / (ClockPeriodNs *10): 
  (12 * (10 ** 6)) / ClockPeriodNs;
  localparam longint TwoMsTimeOut = (2 * (10 ** 6)) / ClockPeriodNs;
  localparam longint OneMsTimeOut = SIM_FAST_LINK ? (1 * (10 ** 4)) / (ClockPeriodNs *10): (1 * (10 ** 6)) / ClockPeriodNs;
  localparam int SixUsTimeOut = (6 * (10 ** 3)) / ClockPeriodNs;
  localparam int EigthHundredNanoSecondTimeOut = (800) / ClockPeriodNs;
  localparam int TwentyNanoSeconds = 20* (10 **0)/ ClockPeriodNs;  //(20 * (10** -9)); //)) / int'((1 / (CLK_RATE * $pow(10, 6))));
  // PCIe requires 1024 transmitted TS1s.  The cocotb link-up test uses the
  // fast-simulation mode so the same state transition can be exercised
  // inside its 25 us timeout.
  localparam int MinTS1sPolling = SIM_FAST_LINK ? 24 : 1024;

  typedef enum logic [19:0] {
    ST_IDLE                           = 20'b00000000000000000000,
    ST_DETECT                         = 20'b00000000000000000001,
    ST_POLLING                        = 20'b00000000000000000010, // 02
    ST_CONFIGURATION                  = 20'b00000000000000000011,
    ST_RECOVERY                       = 20'b00000000000000000100, // 04
    ST_L0                             = 20'b00000000000000000101, // 05
    ST_L0s                            = 20'b00000000000000000110,
    ST_L1                             = 20'b00000000000000000111,
    ST_L2                             = 20'b00000000000000001000,
    ST_DISABLED                       = 20'b00000000000000001001,
    ST_LOOPBACK                       = 20'b00000000000000001010,
    ST_HOT_RESET                      = 20'b00000000000000001011,

    ST_DETECT_WAIT_ONE_MS             = 20'b00000000000000100001, // 21
    ST_DETECT_QUIET                   = 20'b00000000000001000001, // 41
    ST_DETECT_ACTIVE                  = 20'b00000000000001100001, // 61
    ST_DETECT_RX                      = 20'b00000000000010000001, // 81

    ST_POLLING_ACTIVE                 = 20'b00000000000000100010, // 22
    ST_POLLING_CONFIGURATION          = 20'b00000000000001000010, // 42
    ST_POLLING_COMPLIANCE             = 20'b00000000000001100010, // 62

    ST_CONFIGURATION_LINKWIDTH_START  = 20'b00000000000000100011, // 23
    ST_CONFIGURATION_LINKWIDTH_ACCEPT = 20'b00000000000001000011,
    ST_CONFIGURATION_LANENUM_ACCEPT   = 20'b00000000000001100011,
    ST_CONFIGURATION_LANENUM_WAIT     = 20'b00000000000010000011,
    ST_CONFIGURATION_COMPLETE         = 20'b00000000000010100011,
    ST_CONFIGURATION_IDLE             = 20'b00000000000011100011, // E3

    ST_RECOVERY_RCVR_LOCK             = 20'b00000000000000100100, // 24
    ST_RECOVERY_RCVR_LOCK_TIMEOUT     = 20'b00000000000001000100, // 44
    ST_RECOVERY_EQUAL                 = 20'b00000000000001100100, // 64
    ST_RECOVERY_SPEED                 = 20'b00000000000010000100, // 84
    ST_RECOVERY_SPEED_WAIT            = 20'b00000000000010100100, // A4
    ST_RECOVERY_SPEED_EIEOS           = 20'b00000000000011000100, // C4
    ST_RECOVERY_RCVR_CFG              = 20'b00000000000011100100, // E4
    ST_RECOVERY_IDLE                  = 20'b00000000000100000100, //104
    ST_RECOVERY_COMPLETE              = 20'b00000000000100100100, //124
    ST_RECOVERY_EXT_SYNCH             = 20'b00000000000101000100, //144
    ST_RECOVERY_SEND_SDS              = 20'b00000000000101100100, //164
    ST_RECOVERY_EQUAL_PHASE_0         = 20'b00000000000110000100, //184
    ST_RECOVERY_EQUAL_PHASE_1         = 20'b00000000000110100100, //1A4
    ST_RECOVERY_EQUAL_PHASE_2         = 20'b00000000000111000100, //1C4
    ST_RECOVERY_EQUAL_PHASE_3         = 20'b00000000000111100100  //1E4
  } ltssm_state_e;

  typedef struct packed {
    logic equal_complete;
    logic link_equal_req;
    logic phase3_successful;
    logic phase2_successful;
    logic phase1_successful;
    logic phase0_successful;
  } equal_t;

  ltssm_state_e                               curr_state;
  ltssm_state_e                               next_state;
  pcie_ordered_set_t                          ordered_set_c;
  pcie_ordered_set_t                          ordered_set_r;
  logic              [                  63:0] timer_c;
  logic              [                  63:0] timer_r;
  logic                                       error_c;
  logic                                       error_r;
  logic                                       success_c;
  logic                                       success_r;
  logic                                       goto_detect_c;
  logic                                       goto_cfg_c;

  logic              [     MAX_NUM_LANES-1:0] lane_active_c;
  logic              [     MAX_NUM_LANES-1:0] lane_active_r;



  logic              [     MAX_NUM_LANES-1:0] at_least_one_ts1_ts2;
  logic              [     MAX_NUM_LANES-1:0] equal_req;
  logic              [                   7:0] axis_pkt_cnt_c;
  logic              [                   7:0] axis_pkt_cnt_r;
  logic              [                   7:0] try_cnt_c;
  logic              [                   7:0] try_cnt_r;
  rate_id_t                                   curr_data_rate_c;
  rate_id_t                                   curr_data_rate_r;
  rate_id_t                                   last_data_rate_c;
  rate_id_t                                   last_data_rate_r;
  logic                                       successful_speed_negotiation_c;
  logic                                       successful_speed_negotiation_r;
  logic                                       changed_speed_recovery_c;
  logic                                       changed_speed_recovery_r;
  logic                                       equalization_done_8gb_c;
  logic                                       equalization_done_8gb_r;
  logic                                       start_equalization_w_preset_c;
  logic                                       start_equalization_w_preset_r;
  //! internal_axis_signals
  // logic              [   DATA_WIDTH-1:0] ltssm_axis_tdata;
  // logic              [   KEEP_WIDTH-1:0] ltssm_axis_tkeep;
  // logic                                  ltssm_axis_tvalid;
  // logic                                  ltssm_axis_tlast;
  // logic              [   USER_WIDTH-1:0] ltssm_axis_tuser;
  // logic                                  ltssm_axis_tready;

  //!link training helper signals
  logic              [     MAX_NUM_LANES-1:0] link_width_satisfied;
  logic              [     MAX_NUM_LANES-1:0] speed_change_bit_set;
  logic              [                   7:0] link_number_selected;
  logic              [(MAX_NUM_LANES *8)-1:0] link_number_selected_per_lane;
  logic              [   MAX_NUM_LANES-1 : 0] lane_link_number_selected;
  logic              [     MAX_NUM_LANES-1:0] link_lanes_formed;
  logic              [     MAX_NUM_LANES-1:0] lane_num_formed;
  logic              [     MAX_NUM_LANES-1:0] lane_num_satisfied;

  logic              [                  15:0] ordered_set_sent_cnt_c;
  (* mark_debug = "true" *) logic              [                  15:0] ordered_set_sent_cnt_r;

  logic              [     MAX_NUM_LANES-1:0] link_lanes_nums_match;
  logic              [     MAX_NUM_LANES-1:0] link_lane_reconfig;

  logic              [     MAX_NUM_LANES-1:0] ts1_lanenum_wait_satisfied;
  logic              [                   7:0] idle_to_rlock_transitioned_c;
  logic              [                   7:0] idle_to_rlock_transitioned_r;

  logic              [     MAX_NUM_LANES-1:0] lane_status_c;
  logic              [     MAX_NUM_LANES-1:0] lane_status_r;

  // holds last "receiver detected" lines for ST_DETECT_RX state
  logic              [     MAX_NUM_LANES-1:0] lanes_detected_c;
  logic              [     MAX_NUM_LANES-1:0] lanes_detected_r;

  // holds last "receiver elecidle" lines
  logic              [     MAX_NUM_LANES-1:0] phy_rxelecidle_r;
  logic              [     MAX_NUM_LANES-1:0] phy_rxelecidle_exit_detected;

  // Need to pipeline phy_phystatus_i
  logic              [     MAX_NUM_LANES-1:0] phy_phystatus_r;


  logic              [     MAX_NUM_LANES-1:0] phy_rxpolarity_c;
  logic              [     MAX_NUM_LANES-1:0] phy_rxpolarity_r;
  logic              [                15:0] polarity_lockout_timer_c;
  logic              [                15:0] polarity_lockout_timer_r;


  logic                                       link_up_c;
  logic                                       link_up_r;


  (* mark_debug = "true" *) logic              [     MAX_NUM_LANES-1:0] single_idle_received;
  (* mark_debug = "true" *) logic              [     MAX_NUM_LANES-1:0] single_ts1_received;
  (* mark_debug = "true" *) logic              [     MAX_NUM_LANES-1:0] single_ts2_received;
  (* mark_debug = "true" *) logic              [     MAX_NUM_LANES-1:0] link_idle_satisfied;

  //training sequence satisfy signals
  logic              [     MAX_NUM_LANES-1:0] lanes_ts1_satisfied;
  logic              [     MAX_NUM_LANES-1:0] lanes_ts2_satisfied;
  logic              [     MAX_NUM_LANES-1:0] lanes_idle_satisfied;

  logic              [     MAX_NUM_LANES-1:0] ts1_cnt_satisfied;
  logic              [     MAX_NUM_LANES-1:0] ts2_cnt_satisfied;
  logic                                       transmit_ordered_set;
  logic                                       ordered_set_tx_in_process_c;
  logic                                       ordered_set_tx_in_process_r;
  ts2_symbol6_t                               ts2_symbol6;
  rate_id_t                                   rate_id;
  // rate_id = last_data_rate_r;
  rate_speed_e                                max_rate;
  rate_speed_e       [     MAX_NUM_LANES-1:0] max_rate_per_lane;
  logic              [     MAX_NUM_LANES-1:0] lane_max_rate_asserted;
  rate_speed_e                                max_supported_rate_c;
  rate_speed_e                                max_supported_rate_r;
  logic                                       equalization_requested;

  gen_os_struct_t                             gen_os_ctrl_c;
  gen_os_struct_t                             gen_os_ctrl_r;
  presets_coeff_t    [     MAX_NUM_LANES-1:0] preset_coeff_c;
  presets_coeff_t    [     MAX_NUM_LANES-1:0] preset_coeff_r;
  equal_t                                     equal_status_c;
  equal_t                                     equal_status_r;

  assign active_lanes_o         = lane_active_r;
  assign ltssm_state_o          = curr_state;
  assign equalization_requested = (equal_req != '0 | !(equal_status_r.equal_complete));
  assign phy_rxpolarity_o       = phy_rxpolarity_r;
  assign link_up_o              = link_up_r;

 
  always_comb begin : detect_phy_rxelecidle_exit_detected
    for (int i = 0; i < MAX_NUM_LANES; i++) begin
      // If last cycle lane was in elecidle and this cycle it is not,
      // => exit detected
      if (phy_rxelecidle_r[i] && ~phy_rxelecidle_i[i]) begin
        phy_rxelecidle_exit_detected[i] = '1;
      end
      else begin
        phy_rxelecidle_exit_detected[i] = '0;
      end
    end
  end

  always_ff @(posedge clk_i) begin : gen_link_number
    if (rst_i) begin
      link_number_selected <= '0;
      max_rate             <= gen1;
    end else begin
      logic [MAX_NUM_LANES-1:0] flag_lane;
      logic [MAX_NUM_LANES-1:0] flag_rate;
      flag_lane = '0;
      flag_rate = '0;
      for (int i = 0; i < MAX_NUM_LANES; i++) begin
        if (i == 0) begin
          if (lane_link_number_selected[i]) begin
            link_number_selected <= link_number_selected_per_lane[8*i+:8];
          end

          if (lane_max_rate_asserted[i]) begin
            max_rate <= max_rate_per_lane[i];
          end
        end else begin

          if (lane_link_number_selected[i] && ((flag_lane >> i) == '0)) begin
            link_number_selected <= link_number_selected_per_lane[8*i+:8];
            flag_lane[i] = '1;
          end

          if (lane_max_rate_asserted[i] && (flag_rate >> i) == '0) begin
            max_rate <= max_rate_per_lane[i];
            flag_rate[i] = '1;
          end
        end
      end

    end
  end

  //! main sequential block
  always_ff @(posedge clk_i) begin : main_seq
    if (rst_i) begin
      curr_state                     <= ST_IDLE;
      timer_r                        <= '0;
      error_r                        <= '0;
      success_r                      <= '0;
      lane_status_r                  <= '0;
      ordered_set_sent_cnt_r         <= '0;
      axis_pkt_cnt_r                 <= '0;
      try_cnt_r                      <= '0;
      changed_speed_recovery_r       <= '0;
      goto_detect_o                  <= '0;
      goto_cfg_o                     <= '0;
      link_up_r                      <= '0;
      lane_status_r                  <= '0;
      lanes_detected_r               <= '0;
      ordered_set_tx_in_process_r    <= '0;
      lane_active_r                  <= '0;
      equalization_done_8gb_r        <= '0;
      gen_os_ctrl_r.valid            <= '0;
      start_equalization_w_preset_r  <= '1;
      last_data_rate_r               <= gen1_basic;
      curr_data_rate_r               <= gen1_basic;
      preset_coeff_r                 <= '0;
      equal_status_r                 <= '0;
      send_ordered_set_o             <= '0;
      ordered_set_r                  <= pcie_ordered_set_t'('0);
      successful_speed_negotiation_r <= '0;
      idle_to_rlock_transitioned_r   <= '0;
      max_supported_rate_r           <= gen1;
      phy_rxpolarity_r               <= '0;
      polarity_lockout_timer_r       <= '0;
      gen_os_ctrl_r                  <= '0;
      phy_rxelecidle_r               <= '0;
      phy_phystatus_r                <= '0;
      // for(i = 0; i < MAX_NUM_LANES; i++) begin
      //   preset_coeff_r.rx_preset <=
      //   tx_preset <=
      //   pre_cursor
      //   cursor_coef
      // end
    end else begin
      curr_state                     <= next_state;
      phy_rxelecidle_r               <= phy_rxelecidle_i;
      timer_r                        <= timer_c;
      error_r                        <= error_c;
      success_r                      <= success_c;
      lane_status_r                  <= lane_status_c;
      ordered_set_sent_cnt_r         <= ordered_set_sent_cnt_c;
      axis_pkt_cnt_r                 <= axis_pkt_cnt_c;
      try_cnt_r                      <= try_cnt_c;
      last_data_rate_r               <= last_data_rate_c;
      changed_speed_recovery_r       <= changed_speed_recovery_c;
      goto_detect_o                  <= goto_detect_c;
      goto_cfg_o                     <= goto_cfg_c;
      link_up_r                      <= link_up_c;
      lane_status_r                  <= lane_status_c;
      lanes_detected_r               <= lanes_detected_c;
      curr_data_rate_r               <= curr_data_rate_c;
      lane_active_r                  <= lane_active_c;
      equalization_done_8gb_r        <= equalization_done_8gb_c;
      ordered_set_tx_in_process_r    <= ordered_set_tx_in_process_c;
      equal_status_r                 <= equal_status_c;
      start_equalization_w_preset_r  <= start_equalization_w_preset_c;
      send_ordered_set_o             <= transmit_ordered_set;
      ordered_set_r                  <= ordered_set_c;
      successful_speed_negotiation_r <= successful_speed_negotiation_c;
      idle_to_rlock_transitioned_r   <= idle_to_rlock_transitioned_c;
      max_supported_rate_r           <= max_supported_rate_c;
      phy_rxpolarity_r               <= phy_rxpolarity_c;
      polarity_lockout_timer_r       <= polarity_lockout_timer_c;
      gen_os_ctrl_r                  <= gen_os_ctrl_c;
      phy_phystatus_r                <= phy_phystatus_i;
    end
    //non-resetable
  end


  always_comb begin : timer_and_ordered_set_counter
    timer_c = timer_r;
    // ordered_set_sent_cnt_c = ordered_set_sent_cnt_r;
    if (next_state != curr_state && (next_state != ST_RECOVERY_RCVR_LOCK_TIMEOUT)) begin
      timer_c = '0;
      // ordered_set_sent_cnt_c = '0;
    end else begin
      // if (ordered_set_tranmitted_i) begin
      //   ordered_set_sent_cnt_c = ordered_set_sent_cnt_r;
      // end
      timer_c = (timer_r >= FourtyEightMsTimeOut) ? FourtyEightMsTimeOut : timer_r + 1;
    end
  end


  always_comb begin : lane_status
    lane_active_c = lane_active_r;
    if (phy_phystatus_rst_i) begin
      lane_active_c = '0;
    end else begin
      for (int i = 0; i < MAX_NUM_LANES; i++) begin
        if (phy_phystatus_i[i] && phy_rxstatus_i[3*i+:3] == 3'b011) begin
          lane_active_c[i] = '1;
        end
      end
    end
  end



  always_comb begin : ltssm_combo
    next_state                     = curr_state;
    // timer_c                        = timer_r;
    error_c                        = error_r;
    success_c                      = '0;
    lane_status_c                  = lane_status_r;
    lanes_detected_c               = lanes_detected_r;
    ordered_set_sent_cnt_c         = ordered_set_sent_cnt_r;
    try_cnt_c                      = try_cnt_r;
    last_data_rate_c               = last_data_rate_r;
    goto_detect_c                  = goto_detect_o;
    goto_cfg_c                     = goto_cfg_o;
    tx_enter_elec_idle_o           = '0;
    curr_data_rate_c               = curr_data_rate_r;
    ts2_symbol6                    = '0;
    link_up_c                      = '0;
    //ordered set
    ordered_set_c                  = ordered_set_r;
    changed_speed_recovery_c       = changed_speed_recovery_r;
    successful_speed_negotiation_c = successful_speed_negotiation_r;
    idle_to_rlock_transitioned_c   = idle_to_rlock_transitioned_r;
    equalization_done_8gb_c        = equalization_done_8gb_r;
    start_equalization_w_preset_c  = start_equalization_w_preset_r;
    transmit_ordered_set           = '0;
    rate_id                        = last_data_rate_r;
    max_supported_rate_c           = max_supported_rate_r;
    gen_os_ctrl_c                  = gen_os_ctrl_r;
    equal_status_c                 = equal_status_r;
    phy_txdetectrx_o               = '0;
    phy_txelecidle_o               = '0;
    phy_powerdown_o                = '0;
    phy_txdeemph_o                 = '1;
    phy_txcompliance_o             = '0;
    phy_rxpolarity_c               = phy_rxpolarity_r;
    polarity_lockout_timer_c       = (polarity_lockout_timer_r > 0) ? polarity_lockout_timer_r - 1 : 0;
    phy_txmargin_o                 = '0;
    // gen_os_ctrl_c                  = '0;
    case (curr_state)
      //*********************************************************
      // Idle
      //*********************************************************
      // In ST_DETECT_QUIET we need to transmitter to be in  Electrical Idle.
      // Furthermore, the data rate needs to be set to gen1. If that is not the case already, transmit
      // the old rate for one ms (happening in ST_DETECT_WAIT_ONE_MS) and then set the current_data_rate to gen1. 
      // Then proceed to ST_DETECT_QUIET.
      ST_IDLE: begin
        if (en_i) begin
          idle_to_rlock_transitioned_c = '0;
          gen_os_ctrl_c                = '0;
          phy_txelecidle_o             = '1;
          phy_powerdown_o              = 2'b10;
          if (curr_data_rate_r.rate != gen1) begin
            next_state = ST_DETECT_WAIT_ONE_MS;
          end else begin
            next_state = ST_DETECT_QUIET;
          end
        end
      end
      //*********************************************************
      // Detect.Wait.One.Ms
      //*********************************************************
      // Only necessary if data rate is greater than Gen1. This should also set datarate to gen1.
      ST_DETECT_WAIT_ONE_MS: begin
        phy_powerdown_o  = 2'b10;
        phy_txelecidle_o = '1;
        if (timer_r >= OneMsTimeOut) begin
          curr_data_rate_c.rate = gen1;
          next_state = ST_DETECT_QUIET;
        end
      end
      //*********************************************************
      // Detect.Quiet
      //*********************************************************
      // In this state we need to transmit EIs.
      // We leave this state either if 12ms are over, or, if we detect that any receiving lane exits electrical idle.
      // phy_rxelecidle_exit_detected will be 1 for exactly one cycle if any lane exited electrical idle between cycles.
      // Requires an "exit electrical idle" detection
      ST_DETECT_QUIET: begin
        phy_txelecidle_o = '1;
        phy_powerdown_o  = 2'b10;
        phy_txdeemph_o   = '0;

        if (((|phy_rxelecidle_exit_detected) || (timer_r >= TwelveMsTimeOut))) begin
          next_state    = ST_DETECT_ACTIVE;
        end
      end
      //*********************************************************
      // Detect.Active
      //*********************************************************
      // Requires reciever detection to transition to ST_POLLING
      // Receiver detection is triggered in the PIPE. For this, phy_txdetectrx_o has to be set/kept at 1.
      // We then listen on the phy_phystatus_r signal to wait for the receiver detection to finish. 
      // Oddly engough this takes aroun 130 cycles. 
      // The result is stored in receiver_detected_i. If on all lanes a receiver was detected we can transition to ST_POLLING.
      // If only some lanes detect a receiver we go to ST_DETECT_RX. If no receiver were detected we go back to ST_IDLE=>ST_DETECT_QUIET.
      ST_DETECT_ACTIVE: begin
        //bounded timeout counter
        phy_txdetectrx_o = '1;
        phy_powerdown_o  = 2'b10;

        // Wait for receiver detection to finish
        if (|phy_phystatus_r) begin
          if (|receiver_detected_i) begin
            if (&receiver_detected_i) begin
              success_c        = '1;
              // timer_c          = '0;
              lanes_detected_c = receiver_detected_i;
              next_state       = ST_POLLING;
            end else begin
              lanes_detected_c = receiver_detected_i;
              next_state       = ST_DETECT_RX;
            end 
          end else begin
            next_state = ST_IDLE; // Should technically be ST_DETECT_QIUET
          end
        end else if (timer_r >= TwentyFourMsTimeOut) begin
          next_state =  ST_IDLE; // Should technically be ST_DETECT_QIUET
        end
      end
      //*********************************************************
      // Detect.Recever.Detection
      //*********************************************************
      // In this state we need to wait 12ms, and then perform another receiver detection.
      // If the same lanes detect a receiver we can go to ST_POLLING (technically, all undetected lanes need to transition to electrical idle...)
      // If the lanes change we go back to ST_IDLE.
      ST_DETECT_RX: begin
        if (timer_r >= TwelveMsTimeOut) begin
          phy_txdetectrx_o = '1;
          phy_powerdown_o  = 2'b10;
          if (|phy_phystatus_r) begin
            if ((lanes_detected_r == receiver_detected_i)) begin
              success_c        = '1;
              lanes_detected_c = receiver_detected_i;
              next_state       = ST_POLLING;
            end else begin
              error_c    = '1;
              next_state = ST_IDLE;
            end
          end 
        end else if (timer_r >= TwentyFourMsTimeOut) begin
          next_state = ST_IDLE;
        end
      end
      //*********************************************************
      // Polling
      //*********************************************************
      ST_POLLING: begin
        // timer_c                = '0;
        next_state             = ST_POLLING_ACTIVE;
        ordered_set_sent_cnt_c = '0;
        gen_os_ctrl_c          = '0;
        // gen_os_ctrl_c.gen_idle = '1;
        // gen_os_ctrl_c.gen_idle = '1;
        gen_os_ctrl_c.gen_idle = '0;
        gen_os_ctrl_c.valid    = '1;
        gen_os_ctrl_c.gen_ts1  = '1;
        transmit_ordered_set   = '1;
        ordered_set_c = gen_ts_os( gen1, TS1);
      end
      //*********************************************************
      // Polling.Active
      //*********************************************************
      ST_POLLING_ACTIVE: begin
        //bounded timeout counter
        // timer_c = (timer_r >= TwentyFourMsTimeOut) ? TwentyFourMsTimeOut : timer_r + 1;
        //The Transmitter must wait for its TX common mode to settle before exiting from Electrical
        //Idle and transmitting the TS1 Ordered Sets.
        // Phy transmitter handles common mode settling, will throttle with tready
        //check if timer reached or TSOS sent count met
        //check if last packet in frame
        if (ordered_set_tranmitted_i) begin
          // Only start counting after receiving one TS1
          if (|single_ts1_received ) begin
          ordered_set_sent_cnt_c = ordered_set_sent_cnt_r + 1;
          end
          if (|polarity_inverted_i && (polarity_lockout_timer_r == 0)) begin
            phy_rxpolarity_c = phy_rxpolarity_r ^ polarity_inverted_i;
            polarity_lockout_timer_c = 16'd1000; // ~10us lockout
          end

          if ((ordered_set_sent_cnt_r >= MinTS1sPolling)) begin
              if (&lanes_ts1_satisfied || &lanes_ts2_satisfied) begin
                ordered_set_sent_cnt_c = '0;
                //build ts2 ordered set
                gen_os_ctrl_c.gen_ts1 = '0;
                gen_os_ctrl_c.gen_ts2 = '1;
                // ordered_set_c = gen_ts_os( gen1, TS1,PAD_,PAD_,'1);
                ordered_set_c = gen_ts_os( gen1, TS2);
                transmit_ordered_set = '1;
                //goto cofig
                next_state = ST_POLLING_CONFIGURATION;
              end
          end
          if ((timer_r >= TwentyFourMsTimeOut) && (ordered_set_sent_cnt_r >= MinTS1sPolling)) begin
            //reset counts
            // timer_c                = '0;
            ordered_set_sent_cnt_c = '0;
            //check if ts1 reqs satisfied
            if (|lanes_ts1_satisfied || |lanes_ts2_satisfied) begin
              //build ts2 ordered set
              gen_os_ctrl_c.gen_ts1 = '0;
              gen_os_ctrl_c.gen_ts2 = '1;
              // ordered_set_c = gen_ts_os( gen1, TS1,PAD_,PAD_,'1);
              ordered_set_c = gen_ts_os( gen1, TS2);
              transmit_ordered_set = '1;
              //goto cofig
              next_state = ST_POLLING_CONFIGURATION;
            end else if (|lanes_ts1_satisfied) begin
              // TODO: This should be entered when a 24 ms timeout is reached, 1024 TS1s were sent and
              // Any lane received 8 consecutive TS1s with the copmbliance rceive bit of symbol 5 == 1 and loopback bit == 0
              next_state = ST_POLLING_COMPLIANCE;
            end else begin
              // Neither lanes_ts1_satisfied nor lanes_ts2_satisfied is set on
              // any lane -- the link partner never responded at all during
              // Polling.Active. next_state is left alone here (still ==
              // curr_state), so the generic 24ms watchdog below still sends
              // us to ST_IDLE either way; this just makes sure error_o
              // distinguishes "no response at all" from the other paths
              // through this state instead of silently falling through.
              // (1'b1, not '1 -- Verilator 5.050 hits a parser edge case
              // with the unsized literal as the sole statement in a bare
              // else-begin block at this exact position; functionally
              // identical for a 1-bit reg.)
              error_c = 1'b1;
            end
          end
        end  // end of: if (ordered_set_tranmitted_i)

        // 24ms Polling watchdog. Must NOT be gated behind ordered_set_tranmitted_i:
        // a stalled TX handshake is exactly the failure this failsafe exists to
        // catch, and gating it there defeats its purpose (Bug 4).
        // The (next_state == curr_state) guard ensures the watchdog only fires
        // when no success path above has already claimed a transition -- without
        // it, this check would clobber a legitimate ST_POLLING_CONFIGURATION
        // transition that happened to occur at >= 24ms.
        if ((timer_r >= TwentyFourMsTimeOut) && (next_state == curr_state)) begin
          next_state = ST_IDLE;
        end
      end
      //*********************************************************
      // Polling.Compliance: NOT IMPLEMENTED
      //*********************************************************
      ST_POLLING_COMPLIANCE: begin
        //not implemented
        //assert error and go back to deteect low
        error_c    = '1;
        next_state = ST_IDLE;
      end
      //-----------------------------------------------------------
      //  Polling.Configuration
      //-----------------------------------------------------------
      ST_POLLING_CONFIGURATION: begin
        //bounded timeout counter
        if (ordered_set_tranmitted_i && |single_ts2_received) begin
            ordered_set_sent_cnt_c = ordered_set_sent_cnt_r + 1'b1;
        end

        if (|lanes_ts2_satisfied && ordered_set_sent_cnt_r >= 8'h10) begin
          //assert success
          success_c = '1;
          //reset counts
          // timer_c    = '0;
          ordered_set_sent_cnt_c = '0;
          gen_os_ctrl_c.gen_ts1 = '1;
          gen_os_ctrl_c.gen_ts2 = '0;
          transmit_ordered_set = '1;
          ordered_set_c = gen_ts_os( gen1, TS1);
          //goto wait low
          next_state = ST_CONFIGURATION_LINKWIDTH_START;
        end  //check timeout count
        else if (timer_r >= FourtyEightMsTimeOut)
        begin
          // timer_c    = '0;
          //assert error.
          error_c    = '1;
          //goto wait low
          next_state = ST_IDLE;
        end

      end
      //-----------------------------------------------------------
      //  Configuration
      //-----------------------------------------------------------
      ST_CONFIGURATION: begin
        if (ordered_set_tranmitted_i) begin
          ordered_set_sent_cnt_c = ordered_set_sent_cnt_r + 1'b1;
          if (ordered_set_sent_cnt_r >= 4) begin
            gen_os_ctrl_c.gen_ts1  = '1;
            ordered_set_sent_cnt_c = '0;
            transmit_ordered_set = '1;
            next_state             = ST_CONFIGURATION_LINKWIDTH_START;
          end
        end
      end
      //-----------------------------------------------------------
      //  Configuration.Linkwidth.Start
      //-----------------------------------------------------------
      ST_CONFIGURATION_LINKWIDTH_START: begin
        // if (ordered_set_sent_cnt_r) begin
        //   transmit_ordered_set = '1;
        //   ordered_set_c = gen_ts_os( gen1, TS1, train_seq_e'(LINK_NUM));
        // end
        // gen_os_ctrl_c.valid = '1;
        // gen_os_ctrl_c.gen_ts1 = '1;
        if (ordered_set_tranmitted_i) begin
          ordered_set_sent_cnt_c = ordered_set_sent_cnt_r + 1'b1;
          //check if pcie state continue scenario satisfied
          // TODO: Either only wait for two consecutive TS1s with correct link number (|link_width_satisfied)
          // Or send 16-32 TS1s to support crosslink??? ((|link_width_satisfied) && (ordered_set_sent_cnt_r >= 8'h10))
          if ((|link_width_satisfied)) begin
            //reset ordered set sent counter
            ordered_set_sent_cnt_c = '0;
            transmit_ordered_set   = '1;
            //build next ordered set
            ordered_set_c = gen_ts_os( gen1, TS1, train_seq_e'(link_number_selected));
            //goto next pcie ltssm state
            next_state = ST_CONFIGURATION_LINKWIDTH_ACCEPT;
          end
        end  // end of: if (ordered_set_tranmitted_i)

        if ((timer_r >= TwentyFourMsTimeOut) && (next_state == curr_state)) begin
          //assert error
          error_c    = '1;
          //goto detect
          next_state = ST_IDLE;
        end
      end
      //-----------------------------------------------------------
      //  Configuration.Linkwidth.Accept
      //-----------------------------------------------------------
      ST_CONFIGURATION_LINKWIDTH_ACCEPT: begin
        // gen_os_ctrl_c.gen_ts1 = '1;
        //bounded counter for timeout scenario
        gen_os_ctrl_c.valid = '1;
        if ((ordered_set_tranmitted_i)) begin
          ordered_set_sent_cnt_c = ordered_set_sent_cnt_r + 1'b1;
          //check if pcie state continue scenario satisfied
          //link lane formed xor was put for some spec reason, removing for single lane test as it
          //fails to proceed
          if ((|link_lanes_formed) && /*(!(^link_lanes_formed)) &&*/
          ordered_set_sent_cnt_r >= 8'h08)
          begin
            ordered_set_sent_cnt_c = '0;
            gen_os_ctrl_c.gen_ts1  = '1;
            gen_os_ctrl_c.gen_ts2  = '0;
            transmit_ordered_set   = '1;
            ordered_set_c = gen_ts_os( gen1, TS1, train_seq_e'(link_number_selected));
            next_state = ST_CONFIGURATION_LANENUM_WAIT;
          end
        end  // end of: if (ordered_set_tranmitted_i)

        if ((timer_r >= TwoMsTimeOut) && (next_state == curr_state)) begin
          error_c    = '1;
          next_state = ST_IDLE;
        end
      end
      //-----------------------------------------------------------
      // Configuration.Lanenum.Accept
      //-----------------------------------------------------------
      ST_CONFIGURATION_LANENUM_ACCEPT: begin
        //bounded counter for timeout scenario
        if (ordered_set_tranmitted_i) begin
          ordered_set_sent_cnt_c = ordered_set_sent_cnt_r + 1'b1;
          //check if lanes can be formed
          if (|link_lanes_nums_match && ordered_set_sent_cnt_r >= 8'h8) begin
            //build ts2 ordered set
            transmit_ordered_set  = '1;
            gen_os_ctrl_c.gen_ts1 = '0;
            gen_os_ctrl_c.gen_ts2 = '1;
            ordered_set_c = gen_ts_os( gen1, TS2, train_seq_e'(link_number_selected), train_seq_e'(0));
            ordered_set_sent_cnt_c = '0;
            //goto config complete
            next_state = ST_CONFIGURATION_COMPLETE;
          end  //check reconfiguration scenario
          else if (|link_lane_reconfig && ordered_set_sent_cnt_r >= 8'h8)
          begin
            next_state = ST_CONFIGURATION_LANENUM_WAIT;
          end
        end  // end of: if (ordered_set_tranmitted_i)

        if ((timer_r >= TwoMsTimeOut) && (next_state == curr_state)) begin
          //assert error
          error_c    = '1;
          //reset counter
          //goto detect
          next_state = ST_IDLE;
        end
      end
      //-----------------------------------------------------------
      //  Configuration.Lanenum.Wait
      //-----------------------------------------------------------
      ST_CONFIGURATION_LANENUM_WAIT: begin
        if (ordered_set_tranmitted_i) begin
          //check if lane wait exit scenario satisfied
          ordered_set_sent_cnt_c = ordered_set_sent_cnt_r + 1'b1;
          if ((|ts1_lanenum_wait_satisfied)) begin
            // timer_c = '0;
            ordered_set_sent_cnt_c = 0;
            gen_os_ctrl_c.gen_ts1  = '1;
            gen_os_ctrl_c.gen_ts2  = '0;
            transmit_ordered_set   = '1;
            gen_os_ctrl_c.set_lane = '1;
            ordered_set_c = gen_ts_os( gen1, TS1, train_seq_e'(link_number_selected));
            //goto lanenum accept
            next_state = ST_CONFIGURATION_LANENUM_ACCEPT;
          end
        end  // end of: if (ordered_set_tranmitted_i)

        if ((timer_r >= TwoMsTimeOut) && (next_state == curr_state)) begin
          //assert error
          error_c    = '1;
          //goto detect
          next_state = ST_IDLE;
        end
      end
      //-----------------------------------------------------------
      //  Configuration.Complete
      //-----------------------------------------------------------
      ST_CONFIGURATION_COMPLETE: begin
        if (ordered_set_tranmitted_i) begin
          if (|single_ts2_received) begin
          ordered_set_sent_cnt_c = ordered_set_sent_cnt_r + 1'b1;
          end
          //check exit scenario
          if (&lane_num_formed && (ordered_set_sent_cnt_r >= 8'd16)) begin
            //decrement counts
            ordered_set_sent_cnt_c = '0;

            //build idle ordered set
            transmit_ordered_set   = '1;
            ordered_set_c = gen_zeros();
            gen_os_ctrl_c.gen_ts2  = '0;
            gen_os_ctrl_c.gen_ts1  = '0;
            gen_os_ctrl_c.gen_idle = '1;
            //goto config idle
            next_state             = ST_CONFIGURATION_IDLE;
          end
        end  // end of: if (ordered_set_tranmitted_i)

        if ((timer_r >= TwoMsTimeOut) && (next_state == curr_state)) begin
          //assert error
          error_c    = '1;
          //goto idle
          next_state = ST_IDLE;
        end
      end
      //-----------------------------------------------------------
      //  Configuration.Idle
      //-----------------------------------------------------------
      ST_CONFIGURATION_IDLE: begin
        link_up_c = '1;
        //check if idle received
        if (|single_idle_received && ordered_set_tranmitted_i) begin
          //start counting idle OS sent
          ordered_set_sent_cnt_c = ordered_set_sent_cnt_r + 1;
        end
        //check if number of idle OS received and idle OS sent
        if ((&link_idle_satisfied) && (ordered_set_sent_cnt_r >= 8'd16)) begin
          //assert success.. tells ltssm hierarchy to move to its next state            
          success_c                    = '1;
          //reset counters
          ordered_set_sent_cnt_c       = '0;
          gen_os_ctrl_c.gen_ts1        = '0;
          gen_os_ctrl_c.gen_ts2        = '0;
          gen_os_ctrl_c.gen_idle       = '0;
          gen_os_ctrl_c.valid          = '0;
          transmit_ordered_set         = '1;
          idle_to_rlock_transitioned_c = '0;
          //goto wait for ena low
          next_state                   = ST_L0;
        end  //check timeout counter
        else if (timer_r >= TwoMsTimeOut)
        begin
          if (idle_to_rlock_transitioned_r < 8'hFF) begin
            if (curr_data_rate_r == gen1 || curr_data_rate_r == gen2) begin 
              idle_to_rlock_transitioned_c = 8'hFF;
            end else begin
              idle_to_rlock_transitioned_c = idle_to_rlock_transitioned_r + 1;
            end 
            next_state = ST_RECOVERY_RCVR_LOCK;
          end else begin
            idle_to_rlock_transitioned_c = '1;
            //assert error
            error_c                      = '1;
            //goto wait low
            next_state                   = ST_IDLE;
          end
        end
      end
      //-----------------------------------------------------------
      //  L0
      //-----------------------------------------------------------
      ST_L0: begin
        link_up_c = '1;
        success_c = '1;
        transmit_ordered_set         = '1;
        idle_to_rlock_transitioned_c = '0;
        if (|ts1_valid_i || |ts2_valid_i || (directed_speed_change_i && !changed_speed_recovery_r))
        begin
          gen_os_ctrl_c.gen_ts1 = '1;
          gen_os_ctrl_c.valid = '1;
          transmit_ordered_set = '1;
          ordered_set_c = gen_ts_os( curr_data_rate_r.rate, TS1, train_seq_e'(link_number_selected),
                  train_seq_e'(0), last_data_rate_c);
          ordered_set_sent_cnt_c = '0;
          next_state = ST_RECOVERY_RCVR_LOCK;
        end
      end
      //-----------------------------------------------------------
      //  Recovery
      //-----------------------------------------------------------
      ST_RECOVERY: begin
        // timer_c = timer_r + 1'b1;

        if (timer_r >= 8'h0A) begin
          rate_id_t temp_rate_id;
          // timer_c = '0;
          temp_rate_id = gen3_basic;
          gen_os_ctrl_c.gen_ts1 = '1;
          // gen_os_ctrl_c.set_lane = '1;
          gen_os_ctrl_c.valid = '1;
          //if data rate is gen1 and we've tried three times stay at gen1
          if ((last_data_rate_r.rate > gen1) && (try_cnt_r < 8'h3) && !successful_speed_negotiation_r)
          begin
            last_data_rate_c.speed_change = '1;
            temp_rate_id.speed_change = '1;
          end
          transmit_ordered_set = '1;
          ordered_set_c = gen_ts_os( curr_data_rate_r.rate, TS1, train_seq_e'(link_number_selected),
                   train_seq_e'(0), last_data_rate_c);
          ordered_set_sent_cnt_c = '0;
          // if (recovery_i && !is_timeout_i) begin
          // ordered_set_c.rate_id[6] = '1;
          // end
          next_state             = ST_RECOVERY_RCVR_LOCK;
        end
      end
      //-----------------------------------------------------------
      //  Recovery.Lock
      //-----------------------------------------------------------
      ST_RECOVERY_RCVR_LOCK: begin
        //bounded counter for timeout scenario
        ts2_symbol6 = '0;
        if (equalization_requested && curr_data_rate_r.rate == gen3) begin
          next_state = ST_RECOVERY_EQUAL;
        end
        if (|speed_change_bit_set && !changed_speed_recovery_r) begin
          last_data_rate_c.speed_change = '1;
          transmit_ordered_set = '1;
          ordered_set_c = gen_ts_os( curr_data_rate_r.rate, TS1, train_seq_e'(link_number_selected),
                   train_seq_e'(0), last_data_rate_c);
        end
        if (&(ts1_cnt_satisfied | ts2_cnt_satisfied)) begin
          //deassert valid and reset counter
          ordered_set_sent_cnt_c = '0;
          // timer_c                = '0;
          if (extended_synch_i) begin
            //goto next pcie ltssm state
            next_state = ST_RECOVERY_EXT_SYNCH;
          end else begin
            //build next ordered set
            if (max_rate >= gen3) begin
              ts2_symbol6.req_equal = '1;
            end
            gen_os_ctrl_c.gen_ts1 = '0;
            gen_os_ctrl_c.gen_ts2 = '1;
            transmit_ordered_set  = '1;
            ordered_set_c = gen_ts_os( curr_data_rate_r.rate, TS2, train_seq_e'(link_number_selected),
                     train_seq_e'(0), last_data_rate_r, '0, ts2_symbol6);
            //goto next pcie ltssm state
            next_state = ST_RECOVERY_RCVR_CFG;
          end
        end  //check timeout counter
        else if (timer_r >= TwentyFourMsTimeOut)
        begin
          next_state = ST_RECOVERY_RCVR_LOCK_TIMEOUT;
        end
      end
      //-----------------------------------------------------------
      //  Recovery.Rcvr.Lock.Timeout
      //-----------------------------------------------------------
      ST_RECOVERY_RCVR_LOCK_TIMEOUT: begin
        //check secondary config transition
        if ((|((ts1_cnt_satisfied | ts2_cnt_satisfied) & lane_active_r) && (|speed_change_bit_set)) && (
            curr_data_rate_r.rate != gen1 ||
            max_rate != gen1 || last_data_rate_r.rate != gen1))
        begin
          //build next ordered set
          ts2_symbol6 = '0;
          if (max_rate >= gen3) begin
            // ts2_symbol6.req_equal = '1;
          end
          transmit_ordered_set = '1;
          ordered_set_c = gen_ts_os( rate_speed_e'(last_data_rate_r.rate), TS2, train_seq_e'(link_number_selected),
                   train_seq_e'(0), last_data_rate_r, '0, ts2_symbol6);
          //goto next pcie ltssm state
          next_state = ST_RECOVERY_RCVR_CFG;
        end else begin
          if (!changed_speed_recovery_r && curr_data_rate_r.rate != gen1) begin
            transmit_ordered_set = '1;
            ordered_set_c = gen_ts_os( rate_speed_e'(last_data_rate_r.rate), TS2,
                     train_seq_e'(link_number_selected), train_seq_e'(0), last_data_rate_r, '0, ts2_symbol6);
            //goto next pcie ltssm state
            next_state = ST_RECOVERY_SPEED;
          end else if (changed_speed_recovery_r) begin
            //goto next pcie ltssm state
            next_state = ST_RECOVERY_SPEED;
          end else if (changed_speed_recovery_r && (|at_least_one_ts1_ts2)) begin
            //assert error
            error_c    = '1;
            goto_cfg_c = '1;
            //goto detect
            next_state = ST_IDLE;
          end else begin
            //assert error
            error_c       = '1;
            goto_detect_c = '1;
            //goto detect
            next_state    = ST_IDLE;
          end
        end
      end
      ST_RECOVERY_EQUAL: begin
        ts1_symbol6_t temp_ts6;
        ordered_set_sent_cnt_c = '0;
        equal_status_c         = '0;
        // gen_os_ctrl_c          = '0;
        gen_os_ctrl_c.valid    = '1;
        gen_os_ctrl_c.gen_ts2  = '0;
        gen_os_ctrl_c.gen_ts1  = '1;
        // last_data_rate_c.speed_change = '1;
        temp_ts6.ec            = 2'b01;
        transmit_ordered_set   = '1;
        ordered_set_c = gen_ts_os( curr_data_rate_r.rate, TS1, train_seq_e'(link_number_selected),
                 train_seq_e'(0), last_data_rate_c,, temp_ts6);
        next_state = ST_RECOVERY_EQUAL_PHASE_1;
      end
      ST_RECOVERY_EQUAL_PHASE_1: begin
        if (ordered_set_tranmitted_i) begin
          ordered_set_sent_cnt_c = ordered_set_sent_cnt_r + 1'b1;
          if (ordered_set_sent_cnt_r == 32'd31) begin
            gen_os_ctrl_c.gen_ts1    = '0;
            gen_os_ctrl_c.gen3_eieos = '1;
            transmit_ordered_set = '1;
            gen_eieos(ordered_set_c, max_supported_rate_r);
            ordered_set_sent_cnt_c = '0;
            curr_data_rate_c.rate  = gen3;
          end
          if (ordered_set_sent_cnt_r == '0) begin
            ts1_symbol6_t temp_ts6;
            temp_ts6                 = '0;
            gen_os_ctrl_c.gen3_eieos = '0;
            gen_os_ctrl_c.gen_ts1    = '1;
            temp_ts6.ec              = 2'b01;
            transmit_ordered_set     = '1;
            ordered_set_c = gen_ts_os( curr_data_rate_r.rate, TS1, train_seq_e'(link_number_selected),
                     train_seq_e'(0), last_data_rate_c,, temp_ts6);
          end
        end
        if (&(ts1_lanenum_wait_satisfied ^ ~lane_active_r)) begin
          equal_status_c.equal_complete = '1;
          equal_status_c.phase1_successful = '1;
          //skip phase 2 and 3
          //next_state = ST_RECOVERY_EQUAL_PHASE_2;
          next_state = ST_RECOVERY;
        end else if (timer_r >= TwentyFourMsTimeOut) begin
          next_state = ST_RECOVERY_SPEED;
          // timer_c = '0;
        end
      end
      ST_RECOVERY_EQUAL_PHASE_2: begin
        // timer_c = (timer_r >= TwentyFourMsTimeOut) ? TwentyFourMsTimeOut : timer_r + 1;
        if (ts1_cnt_satisfied) begin
          ts1_symbol6_t temp_ts6;
          temp_ts6.ec = 2'b11;
          transmit_ordered_set = '1;
          ordered_set_c = gen_ts_os( curr_data_rate_r.rate, TS1, train_seq_e'(link_number_selected),
                   train_seq_e'(0), last_data_rate_c,, temp_ts6);
          // next_state = ST_RECOVERY_EQUAL;
          // timer_c = '0;
          next_state = ST_RECOVERY_EQUAL_PHASE_3;
        end else if (timer_r >= TwentyFourMsTimeOut) begin
          next_state = ST_RECOVERY_SPEED;
          // timer_c = '0;
        end
      end
      ST_RECOVERY_EQUAL_PHASE_3: begin
        if (ts1_cnt_satisfied) begin
          gen_os_ctrl_c = '0;
          next_state = ST_RECOVERY_RCVR_LOCK;
        end else if (timer_r >= TwentyFourMsTimeOut) begin
          next_state = ST_RECOVERY_SPEED;
          // timer_c = '0;
        end
      end
      //-----------------------------------------------------------
      //  Recovery.Ext.Synch
      //-----------------------------------------------------------
      ST_RECOVERY_EXT_SYNCH: begin
        gen_os_ctrl_c.valid = '1;
        gen_os_ctrl_c.gen_ts1 = '1;
        gen_os_ctrl_c.set_lane = '1;
        //check if last packet in frame
        if (ordered_set_tranmitted_i) begin
          ordered_set_sent_cnt_c = ordered_set_sent_cnt_r + 1'b1;
        end
        //check if pcie state continue scenario satisfied
        if (ordered_set_sent_cnt_r >= 12'd1024) begin
          ts2_symbol6            = '0;
          //deassert valid and reset counter
          ordered_set_sent_cnt_c = '0;
          // timer_c                = '0;
          //build next ordered set
          if (max_rate == gen3) begin
            ts2_symbol6.req_equal = '1;
          end
          // ordered_set_c = gen_ts_os( last_data_rate_r.rate, TS2, link_num_i, lane_num_i, last_data_rate_r, '0,
          //          ts_symbol6_union_t);
          next_state = ST_RECOVERY_RCVR_CFG;
        end
      end
      //recovery speed scenario
      //8 TS2 Ordered on any lane sets with speed_change bit...at_least_one_ts1_ts2
      // and 8 TS2 OS are standard i.e no IEQUES TS2 if gen1/gen2
      //
      //8 consecutive EQ TS2 recived on all configured lanes, speed_change bit
      //set to 1
      //8 consecutive EQ TS2 OS
      //-----------------------------------------------------------
      //  Recovery.Rcvr.Cfg
      //-----------------------------------------------------------
      ST_RECOVERY_RCVR_CFG: begin
        //bounded counter for timeout scenario
        // gen_os_ctrl_c.gen_ts1 = '1;
        // gen_os_ctrl_c.set_lane = '1;
        // timer_c = (timer_r >= TwentyFourMsTimeOut) ? TwentyFourMsTimeOut : timer_r + 1;
        // gen_os_ctrl_c.valid = '1;
        if (ordered_set_tranmitted_i && at_least_one_ts1_ts2) begin
          ordered_set_sent_cnt_c = ordered_set_sent_cnt_r + 1'b1;
        end
        //recovery idle scenario
        if((|(ts2_cnt_satisfied & lane_active_r)
            && (speed_change_bit_set=='0)
            && ordered_set_sent_cnt_r >= 8'd16) && ordered_set_tranmitted_i)
        begin
          successful_speed_negotiation_c = '0;
          gen_os_ctrl_c                  = '0;
          gen_os_ctrl_c.valid            = '1;
          gen_os_ctrl_c.gen_eios         = '0;
          gen_os_ctrl_c.gen_idle         = '1;
          gen_os_ctrl_c.gen_ts1          = '0;
          gen_os_ctrl_c.gen_ts2          = '0;
          // timer_c                        = '0;
          ordered_set_sent_cnt_c         = '0;
          next_state                     = ST_RECOVERY_IDLE;
          transmit_ordered_set           = '1;
          ordered_set_c                  = gen_zeros();
        end
        if((|((ts1_cnt_satisfied || ts2_cnt_satisfied) & lane_active_r)) &&
            (|speed_change_bit_set) &&  (curr_data_rate_r.rate < gen3) &&
            (curr_data_rate_r.rate > gen1 || max_rate > gen1) &&
            ordered_set_sent_cnt_r >= 16'd32)
        begin
          // timer_c                = '0;
          ordered_set_sent_cnt_c = '0;
          for (int i = 0; i < MAX_NUM_LANES; i++) begin
            if (lane_active_r[i]) begin
              if (i == '0) begin
                max_supported_rate_c = last_data_rate_r.rate;
              end else begin
                max_supported_rate_c = max_rate > max_supported_rate_c ? max_supported_rate_c :
                  max_rate;
              end
            end
          end
          if (max_supported_rate_c == gen1) begin
            next_state = ST_RECOVERY_IDLE;
            successful_speed_negotiation_c = '0;
          end else begin
            next_state = ST_RECOVERY_SPEED;
            successful_speed_negotiation_c = '1;
          end
          // timer_c = '0;
          gen_os_ctrl_c          = '0;
          gen_os_ctrl_c.valid    = '1;
          gen_os_ctrl_c.gen_eios = '1;
          ordered_set_sent_cnt_c = '0;
          transmit_ordered_set   = '1;
          gen_eios(ordered_set_c, curr_data_rate_r.rate);
        end
        else if(&(ts1_cnt_satisfied | ts2_cnt_satisfied) && curr_data_rate_r.rate >= gen3
                && (&(speed_change_bit_set ^ lane_active_r)) && ordered_set_sent_cnt_r >= 32'd128)
        begin
          // timer_c                = '0;
          ordered_set_sent_cnt_c = '0;
          for (int i = 0; i < MAX_NUM_LANES; i++) begin
            if (lane_active_r[i]) begin
              if (i == '0) begin
                max_supported_rate_c = last_data_rate_r.rate;
              end else begin
                max_supported_rate_c = max_rate > max_supported_rate_c ? max_supported_rate_c :
                  max_rate;
              end
            end
          end
          gen_os_ctrl_c                  = '0;
          gen_os_ctrl_c.valid            = '1;
          gen_os_ctrl_c.gen_eios         = '1;
          successful_speed_negotiation_c = max_supported_rate_c != gen1;
          transmit_ordered_set           = '1;
          gen_eios(ordered_set_c, curr_data_rate_r.rate);
          next_state = ST_RECOVERY_SPEED;
        end
        if (timer_r >= FourtyEightMsTimeOut) begin
          if (curr_data_rate_r.rate == gen1 || curr_data_rate_r.rate == gen2) begin
            next_state = ST_IDLE;
          end else if (idle_to_rlock_transitioned_r < 8'hFF && curr_data_rate_r.rate >= gen3) begin
            changed_speed_recovery_c = '0;
            next_state = ST_RECOVERY_IDLE;
          end else begin
            next_state = ST_IDLE;
          end;
        end
      end
      //-----------------------------------------------------------
      //  Recovery.Speed
      //-----------------------------------------------------------
      ST_RECOVERY_SPEED: begin
        tx_enter_elec_idle_o = '1;
        gen_os_ctrl_c.gen_ts1 = '1;
        gen_os_ctrl_c.set_lane = '1;
        // curr_data_rate_c.rate = max_supported_rate_r;
        //bounded counter for timeout scenario
        // timer_c = (timer_r >= TwentyFourMsTimeOut) ? TwentyFourMsTimeOut : timer_r + 1;
        gen_os_ctrl_c.valid = '1;
        // if (ordered_set_tranmitted_i) begin
        //   ordered_set_sent_cnt_c = ordered_set_sent_cnt_r + 1'b1;
        // end

        // if (curr_data_rate_r.rate == gen1 || curr_data_rate_r.rate == gen3) begin
        //   if (ordered_set_sent_cnt_r >= 8'h1) begin
        //     gen_os_ctrl_c.valid = '0;
        //   end
        //   if (&(phy_rxelecidle_i | ~lane_active_r)) begin
        //     //bounded counter for timeout scenario
        //     gen_os_ctrl_c.valid = '0;
        //     next_state = ST_RECOVERY_SPEED_WAIT;
        //   end
        // end else begin
        //   if (ordered_set_sent_cnt_r >= 8'h2) begin
        //     gen_os_ctrl_c.valid = '0;
        //   end
        //   if (&(phy_rxelecidle_i | ~lane_active_r)) begin
        //     gen_os_ctrl_c.valid = '0;
        //     //bounded counter for timeout scenario
        //     next_state = ST_RECOVERY_SPEED_WAIT;
        //   end
        // end
        if (ordered_set_tranmitted_i) begin
          ordered_set_sent_cnt_c = ordered_set_sent_cnt_r + 1'b1;
        end
        if (&(phy_rxelecidle_i | ~lane_active_r) && ordered_set_sent_cnt_r >= 2) begin
          gen_os_ctrl_c.valid = '0;
          //bounded counter for timeout scenario
          next_state = ST_RECOVERY_SPEED_WAIT;
        end
        //check timeout counter
        if (timer_r >= FourtyEightMsTimeOut) begin
          next_state = ST_IDLE;
        end
      end
      //-----------------------------------------------------------
      //  Recovery.Speed.Wait
      //-----------------------------------------------------------
      ST_RECOVERY_SPEED_WAIT: begin
        //bounded counter for timeout scenario
        // timer_c = (timer_r >= TwentyFourMsTimeOut) ? TwentyFourMsTimeOut : timer_r + 1;
        if (successful_speed_negotiation_r) begin
          last_data_rate_c = '0;
          if (timer_r >= EigthHundredNanoSecondTimeOut) begin
            curr_data_rate_c.rate    = max_supported_rate_r;
            last_data_rate_c.rate    = max_supported_rate_r;
            changed_speed_recovery_c = '1;
            if (max_supported_rate_r >= gen3) begin
              gen_os_ctrl_c.valid      = '1;
              gen_os_ctrl_c.gen3_eieos = '1;
              next_state               = ST_RECOVERY_SPEED_EIEOS;
              transmit_ordered_set     = '1;
              gen_eieos(ordered_set_c, max_supported_rate_r);
              ordered_set_sent_cnt_c = '0;
            end else begin
              next_state = ST_RECOVERY_RCVR_LOCK;
              transmit_ordered_set = '1;
              ordered_set_c = gen_ts_os( last_data_rate_c.rate, TS1,
                       train_seq_e'(link_number_selected), train_seq_e'(0), last_data_rate_c);
            end
          end
        end else if (timer_r >= SixUsTimeOut) begin
          changed_speed_recovery_c = '0;
          curr_data_rate_c         = curr_data_rate_r;
          last_data_rate_c         = curr_data_rate_r.rate;
          transmit_ordered_set     = '1;
          ordered_set_c = gen_ts_os( last_data_rate_c.rate, TS1, train_seq_e'(link_number_selected),
                   train_seq_e'(0), last_data_rate_c);
          next_state = ST_RECOVERY_RCVR_LOCK;
        end
      end
      //-----------------------------------------------------------
      //  Recovery.Speed.Eieos
      //-----------------------------------------------------------
      //this state exists to ensure that eieos is transmitted before going into tx elec idle
      ST_RECOVERY_SPEED_EIEOS: begin
        gen_os_ctrl_c = '0;
        gen_os_ctrl_c.valid = '1;
        if (ordered_set_tranmitted_i) begin
          ordered_set_sent_cnt_c = ordered_set_sent_cnt_r + 1'b1;
        end
        if (ordered_set_sent_cnt_r >= 8'h8) begin
          next_state = ST_RECOVERY_RCVR_LOCK;
          transmit_ordered_set = '1;
          ordered_set_c = gen_ts_os( last_data_rate_r.rate, TS1, train_seq_e'(link_number_selected),
                   train_seq_e'(0), last_data_rate_r);
        end
      end
      //-----------------------------------------------------------
      //  Recovery.Idle
      //-----------------------------------------------------------
      ST_RECOVERY_IDLE: begin
        //bounded counter for timeout scenario
        // timer_c = (timer_r >= TwentyFourMsTimeOut) ? TwentyFourMsTimeOut : timer_r + 1;
        gen_os_ctrl_c.valid = '1;
        if (ordered_set_tranmitted_i) begin
          if (single_idle_received) begin
            ordered_set_sent_cnt_c = ordered_set_sent_cnt_r + 1'b1;
          end
        end
        if (((|lanes_idle_satisfied) && ordered_set_sent_cnt_r >= 8'd16)) begin
        gen_os_ctrl_c                = '0;
        gen_os_ctrl_c.valid          = '0;
        next_state                   = ST_L0;
        idle_to_rlock_transitioned_c = '0;
        end else if (at_least_one_ts1_ts2) begin
          // timer_c                = '0;
          gen_os_ctrl_c.valid        = '1;
          ordered_set_sent_cnt_c     = '0;
          gen_os_ctrl_c.gen_ts1      = '1;
          gen_os_ctrl_c.gen_ts2      = '0;
          transmit_ordered_set       = '1;
          ordered_set_c = gen_ts_os(gen1, TS1);
          next_state             = ST_CONFIGURATION_LINKWIDTH_START;
        end else if (timer_r >= TwoMsTimeOut) begin
          //goto recovery scenario
          if (idle_to_rlock_transitioned_r != '1) begin
            // timer_c                = '0;
            gen_os_ctrl_c.valid    = '0;
            ordered_set_sent_cnt_c = '0;
            //check data rate for retry options
            if (curr_data_rate_r.rate == gen1) begin
              idle_to_rlock_transitioned_c = idle_to_rlock_transitioned_r == '1 ?
              '1 : idle_to_rlock_transitioned_r + 1'b1;
            end
            if (curr_data_rate_r.rate == gen2) begin
              idle_to_rlock_transitioned_c = '1;
            end
            next_state = ST_RECOVERY;
          end  //goto detect
          else
          begin
            // timer_c                = '0;
            gen_os_ctrl_c.valid    = '0;
            ordered_set_sent_cnt_c = '0;
            next_state             = ST_IDLE;
          end
        end
      end
      //-----------------------------------------------------------
      //  Recovery.Send.SDS
      //-----------------------------------------------------------
      ST_RECOVERY_SEND_SDS: begin
        gen_os_ctrl_c.valid = '1;
        if (ordered_set_tranmitted_i) begin
          idle_to_rlock_transitioned_c = '0;
          gen_os_ctrl_c.valid          = '0;
          next_state                   = ST_L0;
        end
      end
      default: begin
      end
    endcase
  end


  //-----------------------------------------------------------
  //  Lane based Ordered set handling logic
  //-----------------------------------------------------------
  for (genvar lane = 0; lane < MAX_NUM_LANES; lane++) begin : gen_cnt_ts1
    //local helper counters
    (* mark_debug = "true" *) logic              [7:0] ts1_cnt;
    (* mark_debug = "true" *) logic              [7:0] ts2_cnt;
    logic              [7:0] idle_cnt;

    logic              [7:0] lane_in_save;
    logic                    first_ts1;
    ts_symbol6_union_t       temp_ts6;
    rate_id_t                temp_rate_id;
    logic                    lane_speed_change_bit;

    // Signals used for combinatorial logic block
    logic [7:0] ts1_cnt_c, ts2_cnt_c, idle_cnt_c;
    logic first_ts1_c;

    logic single_idle_received_c;
    logic single_ts1_received_c;
    logic single_ts2_received_c;

    logic lane_link_number_selected_c;
    logic lane_max_rate_asserted_c;
    logic lane_speed_change_bit_c;

    logic [7:0] link_number_selected_per_lane_c;
    logic [7:0] lane_in_save_c;
    ts_symbol6_union_t temp_ts6_c;

    rate_id_t temp_rate_id_c;

    rate_speed_e max_rate_per_lane_c;


    always_ff @(posedge clk_i) begin : output_registers
      if (rst_i) begin
        //determine if TS1 req satisfied for lane by its count
        link_width_satisfied[lane]       <= '0;
        //determine if TS1 req satisfied for lane by its count
        link_lanes_formed[lane]          <= '0;
        //determine if TS1 req satisfied
        ts1_lanenum_wait_satisfied[lane] <= '0;
        link_lanes_nums_match[lane]      <= '0;
        link_lane_reconfig[lane]         <= '0;
        lane_num_formed[lane]            <= '0;
        //determine if TS1 req satisfied for lane by its count
        link_idle_satisfied[lane]        <= '0;
        ts1_cnt_satisfied[lane]          <= '0;
        ts2_cnt_satisfied[lane]          <= '0;
        at_least_one_ts1_ts2[lane]       <= '0;
        //assignments for state exit scenarios
        lanes_ts1_satisfied[lane]        <= '0;
        lanes_ts2_satisfied[lane]        <= '0;
        lanes_idle_satisfied[lane]       <= '0;
        speed_change_bit_set[lane]       <= '0;
      end else begin
        //determine if TS1 req satisfied for lane by its count
        link_width_satisfied[lane]       <= (ts1_cnt >= 8'h2) | (ts2_cnt == 8'h2);
        //determine if TS1 req satisfied for lane by its count
        link_lanes_formed[lane]          <= (ts1_cnt >= 8'h2);
        //determine if TS1 req satisfied
        ts1_lanenum_wait_satisfied[lane] <= (ts1_cnt >= 8'h2);
        link_lanes_nums_match[lane]      <= (ts1_cnt >= 8'h2) | (ts2_cnt >= 8'h2);
        link_lane_reconfig[lane]         <= (ts1_cnt >= 8'h2);
        lane_num_formed[lane]            <= lane_active_r[lane] ? (ts2_cnt == 8'h8) : '1;
        //determine if TS1 req satisfied for lane by its count
        //(ts1_cnt is repurposed as the idle count while curr_state ==
        //ST_CONFIGURATION_IDLE -- see that state's per-lane block, same
        //convention ST_RECOVERY_IDLE uses for its own counter -- so this is
        //not a mixup with idle_cnt/lanes_idle_satisfied, which belong to
        //ST_RECOVERY_IDLE's separate exit condition instead.) Gated by
        //lane_active_r like its siblings above/below so an inactive lane on
        //a reduced-width link contributes a trivial '1' to the &-reduction
        //at ST_CONFIGURATION_IDLE's exit check instead of blocking it
        //forever.
        link_idle_satisfied[lane]        <= lane_active_r[lane] ? (ts1_cnt >= 8'h8) : '1;
        ts1_cnt_satisfied[lane]          <= lane_active_r[lane] ? (ts1_cnt == 8'h8) : '1;
        ts2_cnt_satisfied[lane]          <= lane_active_r[lane] ? (ts2_cnt == 8'h8) : '1;
        at_least_one_ts1_ts2[lane]       <= (ts1_cnt_c != '0) | (ts2_cnt_c != '0);
        //assignments for state exit scenarios
        lanes_ts1_satisfied[lane]        <= receiver_detected_i[lane] ? (ts1_cnt == 8'h8) : '1;
        lanes_ts2_satisfied[lane]        <= receiver_detected_i[lane] ? (ts2_cnt == 8'h8) : '1;
        lanes_idle_satisfied[lane]       <= idle_cnt >= 8'h8;
        speed_change_bit_set[lane]       <= lane_speed_change_bit != '0;
      end

    end

    always_ff @(posedge clk_i) begin
      if (rst_i) begin
        ts1_cnt                                  <= '0;
        ts2_cnt                                  <= '0;
        idle_cnt                                 <= '0;
        first_ts1                                 <= '0;
        link_number_selected_per_lane[lane*8+:8] <= '0;
        lane_in_save                             <= PAD_;
        single_idle_received[lane]               <= '0;
        single_ts1_received[lane]                <= '0;
        single_ts2_received[lane]                <= '0;
        temp_ts6                                 <= '0;
        lane_speed_change_bit                    <= '0;
        max_rate_per_lane[lane]                  <= gen1;
        lane_max_rate_asserted[lane]             <= '0;
      end else begin
        ts1_cnt <= ts1_cnt_c;
        ts2_cnt <= ts2_cnt_c;
        idle_cnt <= idle_cnt_c;
        first_ts1 <= first_ts1_c;

        single_idle_received[lane] <= single_idle_received_c;
        single_ts1_received[lane]  <= single_ts1_received_c;
        single_ts2_received[lane]  <= single_ts2_received_c;

        lane_speed_change_bit <= lane_speed_change_bit_c;

        lane_link_number_selected[lane] <= lane_link_number_selected_c;
        lane_max_rate_asserted[lane]    <= lane_max_rate_asserted_c;

        link_number_selected_per_lane[lane*8+:8] <= link_number_selected_per_lane_c;
        lane_in_save <= lane_in_save_c;
        max_rate_per_lane[lane] <= max_rate_per_lane_c;
        temp_ts6 <= temp_ts6_c;
        temp_rate_id <= temp_rate_id_c;

      end
    end


    always_comb begin
      // =========================
      // DEFAULTS (HOLD)
      // =========================
      ts1_cnt_c  = ts1_cnt;
      ts2_cnt_c  = ts2_cnt;
      idle_cnt_c = idle_cnt;
      first_ts1_c = first_ts1;

      single_idle_received_c = single_idle_received[lane];
      single_ts1_received_c  = single_ts1_received[lane];
      single_ts2_received_c  = single_ts2_received[lane];

      lane_speed_change_bit_c = lane_speed_change_bit;

      lane_link_number_selected_c = '0;
      lane_max_rate_asserted_c    = '0;

      link_number_selected_per_lane_c = link_number_selected_per_lane[lane*8+:8];
      lane_in_save_c = lane_in_save;
      max_rate_per_lane_c = max_rate_per_lane[lane];

      temp_ts6_c = temp_ts6;
      temp_rate_id_c = temp_rate_id;

      // =========================
      // GLOBAL TRANSITION RESET
      // =========================
      if (next_state != curr_state &&
          next_state != ST_RECOVERY_RCVR_LOCK_TIMEOUT) begin

        ts1_cnt_c  = '0;
        ts2_cnt_c  = '0;
        idle_cnt_c = '0;
        first_ts1_c = '0;

        single_idle_received_c = '0;
        single_ts1_received_c  = '0;
        single_ts2_received_c  = '0;

        lane_speed_change_bit_c = '0;

      end else begin

        case (curr_state)

          // =========================
          ST_IDLE: begin
            ts1_cnt_c ='0;
            ts2_cnt_c ='0;
            idle_cnt_c ='0;
            first_ts1_c ='0;

            single_idle_received_c ='0;
            single_ts1_received_c  ='0;
            single_ts2_received_c  ='0;
          end

          // =========================
          ST_POLLING_ACTIVE: begin
            if (ts1_valid_i[lane]) begin
              single_ts1_received_c = '1;

              if ((ordered_set_i[lane].link_num == PAD) && (ordered_set_i[lane].lane_num == PAD)) begin
                ts1_cnt_c = (ts1_cnt >= 8'h8) ? 8'h8 : ts1_cnt + 1;
              end else begin
                ts1_cnt_c = ts1_cnt >= 8'h8 ? 8'h8 : '0;
              end
            end else if (ts2_valid_i[lane]) begin
              single_ts2_received_c = '1;

              if ((ordered_set_i[lane].link_num == PAD) && (ordered_set_i[lane].lane_num == PAD)) begin 
                ts2_cnt_c = (ts2_cnt >= 8'h8) ? 8'h8 : ts2_cnt + 1;
              end else begin
                ts2_cnt_c = ts2_cnt >= 8'h8 ? 8'h8 : '0;
              end
            end
          end

          // =========================
          ST_POLLING_CONFIGURATION: begin
            if (ts2_valid_i[lane]) begin
              single_ts2_received_c ='1;

              if ((ordered_set_i[lane].link_num == PAD) && (ordered_set_i[lane].lane_num == PAD))
                ts2_cnt_c = (ts2_cnt >= 8'h8) ? 8'h8 : ts2_cnt + 1;
              else
                ts2_cnt_c = ts2_cnt >= 8'h8 ? 8'h8 : '0;
            end
          end

          // =========================
          ST_RECOVERY_RCVR_LOCK,
          ST_RECOVERY_RCVR_LOCK_TIMEOUT: begin
            //wait for incoming ts1-os...//skip if threshhold already reached
            if (ts1_valid_i[lane]) begin
              single_ts1_received_c ='1;
            end else if (ts2_valid_i[lane]) begin
              single_ts2_received_c ='1;
            end

            if (ts1_valid_i[lane] || ts2_valid_i[lane]) begin

              if (lane == '0) begin
                max_rate_per_lane_c =
                  (ordered_set_i[lane].rate_id.rate > max_rate)
                  ? ordered_set_i[lane].rate_id.rate : max_rate;

                lane_max_rate_asserted_c ='1;
              end

              ts1_cnt_c = ts1_cnt >= 8'h8 ? 8'h8 : ts1_cnt + 1;

              lane_speed_change_bit_c =
                ordered_set_i[lane].rate_id.speed_change;
            end
          end

          // =========================
          ST_RECOVERY_RCVR_CFG: begin
            //wait for incoming ts1-os...//skip if threshhold already reached
            if (ts2_valid_i[lane]) begin
              single_ts2_received_c ='1;

              if ((temp_ts6 == ordered_set_i[lane].ts_s6) &&
                  ((curr_data_rate_r.rate < gen3) ||
                  ((curr_data_rate_r.rate >= gen3) &&
                    ordered_set_i[lane].ts_s6.ts2.req_equal)) &&
                  (temp_rate_id == ordered_set_i[lane].rate_id) ||
                  !first_ts1) begin
                temp_ts6_c = ordered_set_i[lane].ts_s6;
                ts2_cnt_c = (ts2_cnt >= 8'h8) ? 8'h8 : ts2_cnt + 1;
                first_ts1_c ='1;
                temp_rate_id_c = ordered_set_i[lane].rate_id;

                lane_speed_change_bit_c =
                  ordered_set_i[lane].rate_id.speed_change;

              end else begin
                ts2_cnt_c = (ts2_cnt >= 8'h8) ? 8'h8 : '0;
                first_ts1_c ='0;
                lane_speed_change_bit_c ='0;
              end
            end
          end

          // =========================
          ST_RECOVERY_EQUAL_PHASE_1: begin
            if (ts1_valid_i[lane]) begin
              single_ts1_received_c ='1;

              if (ordered_set_i[lane].ts_s6.ts1.ec == 2'b01) begin
                ts1_cnt_c = (ts1_cnt >= 8'h2) ? 8'h2 : ts1_cnt + 1;
                single_idle_received_c ='0;
              end
            end
          end

          // =========================
          ST_RECOVERY_IDLE: begin
            //wait for incoming ts1-os...//skip if threshhold already reached
            //using ts1_cnt as idle count
            if (idle_valid_i[lane]) begin
              single_idle_received_c ='1;
              idle_cnt_c = (idle_cnt >= 8'h8) ? 8'h8 : idle_cnt + 1;

            end else if (ts1_valid_i[lane] || ts2_valid_i[lane]) begin
              idle_cnt_c = idle_cnt >= 8'h8 ? 8'h8 : '0;
            end

            if ((ts1_valid_i[lane] || ts2_valid_i[lane]) &&
                (ordered_set_i[lane].lane_num == PAD)) begin
              ts2_cnt_c = (ts2_cnt >= 8'h8) ? 8'h8 : ts2_cnt + 1;
              single_idle_received_c ='0;
            end
          end

          // =========================
          ST_CONFIGURATION_LINKWIDTH_START: begin
            //wait for incoming ts1-os...//skip if threshhold already reached
            if (ts1_valid_i[lane]) begin
              single_ts1_received_c ='1;

              if ((ordered_set_i[lane].link_num == PAD) &&
                  (ordered_set_i[lane].lane_num == PAD)) begin
                first_ts1_c ='1;
              end

              
              //check that link number is not pad and that lane number is pad
              if ((ordered_set_i[lane].link_num != PAD) &&
                  (ordered_set_i[lane].lane_num == PAD)) begin
                //incrment ts1 count
                ts1_cnt_c = (ts1_cnt >= 8'h2) ? 8'h2 : ts1_cnt + 1;
              end else begin    
                //reset ts1 cnt... this ensures that the TS1-OS are consecutive per the spec
                ts1_cnt_c = (ts1_cnt >= 8'h2) ? 8'h2 :'0;
              end
            end

            //check if consecutive TS1's satisfied for this lane
            if (link_width_satisfied[lane]) begin
              //select link number by choosing the lowest-numbered lane that
              //is currently satisfied -- not hardcoded to lane 0.
              //(1<<lane)-1 masks off every bit at position >= lane, leaving
              //just the lanes below it; for lane==0 this mask is 0 so the
              //check is trivially true (there is no lower lane), which is
              //why no separate lane==0 special case is needed here (and
              //avoids an invalid link_width_satisfied[lane-1:0] part-select
              //at lane==0, since `lane` is a genvar -- elaborated per
              //instance, not a runtime index -- and that range would
              //elaborate to [-1:0] for that instance).
              if ((link_width_satisfied & ((1 << lane) - 1)) == '0) begin
                link_number_selected_per_lane_c = ordered_set_i[lane].link_num;
                lane_link_number_selected_c ='1;
              end
            end
          end

          // =========================
          ST_CONFIGURATION_LINKWIDTH_ACCEPT: begin
            //wait for incoming ts1-os...//skip if threshhold already reached
            if (ts1_valid_i[lane]) begin
              single_ts1_received_c ='1;

              //check that incoming link number matches the "link_number_selected"
              //that we are now transmitting and that lane number is different
              //from the one stored when we entered this state
              if (ordered_set_i[lane].link_num == link_number_selected) begin
                //increment count
                ts1_cnt_c = (ts1_cnt >= 8'h2) ? 8'h2 : ts1_cnt + 1;
                lane_in_save_c = ordered_set_i[lane].lane_num;
              end else begin
                ts1_cnt_c = (ts1_cnt >= 8'h2) ? 8'h2 : '0;
              end
            end
          end

          // =========================
          ST_CONFIGURATION_LANENUM_WAIT: begin
            if (ts1_valid_i[lane]) begin
              single_ts1_received_c ='1;
            end else if (ts2_valid_i[lane]) begin
              single_ts2_received_c ='1;
            end

            if (ts1_valid_i[lane] || ts2_valid_i[lane]) begin
              if ((ordered_set_i[lane].link_num != PAD) &&
                  (ordered_set_i[lane].lane_num != lane_in_save)) begin
                ts1_cnt_c = (ts1_cnt >= 8'h2) ? 8'h2 : ts1_cnt + 1;
              end else begin    
                ts1_cnt_c = (ts1_cnt >= 8'h2) ? 8'h2 : '0;
              end
            end
          end

          // =========================
          ST_CONFIGURATION_LANENUM_ACCEPT: begin
            if (ts1_valid_i[lane])
              single_ts1_received_c ='1;
            else if (ts2_valid_i[lane])
              single_ts2_received_c ='1;

            if (ts1_valid_i[lane] || ts2_valid_i[lane]) begin
              if ((ordered_set_i[lane].link_num == link_number_selected) &&
                  (ordered_set_i[lane].lane_num != PAD)) begin

                ts1_cnt_c = (ts1_cnt >= 8'h2) ? 8'h2 : ts1_cnt + 1;

                if (lane == '0) begin
                  max_rate_per_lane_c =
                    (ordered_set_i[lane].rate_id.rate > max_rate)
                    ? ordered_set_i[lane].rate_id.rate : max_rate;

                  lane_max_rate_asserted_c ='1;
                end

              end else begin
                ts1_cnt_c = (ts1_cnt >= 8'h2) ? 8'h2 : '0;
              end
            end
          end

          // =========================
          ST_CONFIGURATION_COMPLETE: begin
            if (ts2_valid_i[lane]) begin
              single_ts2_received_c ='1;

              if ((ordered_set_i[lane].link_num == link_number_selected) &&
                  (ordered_set_i[lane].lane_num == lane)) begin
                ts2_cnt_c = (ts2_cnt >= 8'h8) ? 8'h8 : ts2_cnt + 1;
                ts1_cnt_c = '0;
              end else begin
                ts1_cnt_c = '0;
                ts2_cnt_c = (ts2_cnt >= 8'h8) ? 8'h8 : '0;
              end
            end
          end

          // =========================
          ST_CONFIGURATION_IDLE: begin
            //wait for incoming ts1-os...//skip if threshhold already reached
            //using ts1_cnt as idle count
            if (idle_valid_i[lane]) begin
              single_idle_received_c ='1;
              ts1_cnt_c = (ts1_cnt >= 8'h8) ? 8'h8 : ts1_cnt + 1;

            end else if (ts1_valid_i[lane] || ts2_valid_i[lane]) begin
              ts1_cnt_c = (ts1_cnt >= 8'h8) ? 8'h8 : '0;
            end
          end

          default: ;

        endcase
      end
    end
  end

  assign ordered_set_o    = ordered_set_r;
  assign curr_data_rate_o = curr_data_rate_r.rate;
  assign gen_os_ctrl_o    = gen_os_ctrl_r;

endmodule
